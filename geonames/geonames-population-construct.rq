PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sdmxdim: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX ocdp: <http://harth.org/andreas/2017/ocdp/vocabs/ocdp#>
PREFIX estat: <http://ontologycentral.com/2009/01/eurostat/ns#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX gn: <http://www.geonames.org/ontology#>

CONSTRUCT {
   ?city rdfs:label ?alabel , ?olabel .
   ?city ocdp:population _:bn .
   _:bn rdf:value ?population .
   # assume the population values are up to date
   _:bn dcterms:date "2017" .

   ?city ocdp:country ?country .
   ?country rdfs:label ?countrylabel .
   ?country ocdp:isoAlpha3 ?iso3 .
}
WHERE {
  ?city gn:population ?population .
  ?city gn:officialName ?olabel .
  ?city gn:alternateName ?alabel .
  ?city gn:parentCountry ?country .
  ?country gn:officialName ?countrylabel .
  ?country gn:countryCode ?iso2 .

  ?wcountry ocdp:isoAlpha2 ?iso2 ; ocdp:isoAlpha3 ?iso3 .
}