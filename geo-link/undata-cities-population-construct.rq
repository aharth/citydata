PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sdmxdim: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX ocdp: <http://harth.org/andreas/2017/ocdp/vocabs/ocdp#>
PREFIX dcterms: <http://purl.org/dc/terms/>

CONSTRUCT {
   ?city rdfs:label ?citylabel .
   ?city ocdp:population _:bn .
   _:bn rdf:value ?population ; dcterms:date ?year .
}
WHERE {
  ?x sdmxdim:refArea ?city ;
     <http://citydata.wu.ac.at/ns#refAreaName> ?citylabel ;
     <http://citydata.wu.ac.at/ns#hasIndicator> <http://citydata.wu.ac.at/ns#population> ;
     <http://purl.org/linked-data/sdmx/2009/measure#obsValue> ?population ;
     <http://purl.org/dc/terms/date> ?year .
}